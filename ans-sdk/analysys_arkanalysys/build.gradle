apply plugin: 'com.android.library'
apply from: "../maven.gradle"

android {

    compileSdkVersion rootProject.ext.compileSdkVersion

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), '../proguard-base.pro', 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.sourceJavaVersion
        targetCompatibility rootProject.ext.targetJavaVersion
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
    }
}

dependencies {
    api project(':analysys_core')
    api project(':analysys_encryption')
    api project(':analysys_push')
    api project(':analysys_visual')
    api project(':analysys_allgro')
}

task delRelease(type: Delete) {
    delete('../release')
    delete('../../repo')

//    delete('../../repo/cn/com/analysys/analysys-core/')
//    delete('../../repo/cn/com/analysys/analysys-encrypt/')
//    delete('../../repo/cn/com/analysys/analysys-push/')
//    delete('../../repo/cn/com/analysys/analysys-visual/')
//    delete('../../repo/cn/com/analysys/analysys-allgro/')
//    delete('../../repo/cn/com/analysys/analysys-arkanalysys/')
}

task releaseAar(type: Copy) {

    for (int i = 0; i < moduleName.size; i++) {
        def tmpName = "${moduleName[i]}".replace("_${rootProject.ext.version}", "")

        def endName = "${moduleName[i]}".replace("_","-")
        from("../${tmpName}/build/outputs/aar/") {
            rename("${tmpName}-release.aar", "${endName}.aar")
            exclude("${tmpName}-release.aar.asc")
        }
    }

    includeEmptyDirs = false

    into '../release/aar/'

}

task releaseMapping(type: Copy, dependsOn: ['releaseAar']) {

    for (int i = 0; i < moduleName.size; i++) {

        def tmpName = "${moduleName[i]}".replace("_${rootProject.ext.version}", "")

        def endName = "${moduleName[i]}".replace("_","-")
        from("../${tmpName}/build/outputs/mapping/release/") {
            include('mapping.txt')
            rename('mapping.txt', "mapping-${endName}.txt")
        }
    }

    into '../release/mapping/'
}

task zipMapping(type: Zip,dependsOn: ['releaseMapping']) {
    from '../release/mapping/'

    baseName = "analysys-pass-mapping-${releaseTimeVariable}-${rootProject.ext.version}"
    destinationDir file("../release/zip")
}


for (int i = 0; i < moduleName.size(); i++) {
    def endName = "${moduleName[i]}".replace("_","-")

    task "unZipAAR${moduleName[i]}"(type: Copy) {
        from(zipTree("../release/aar/${endName}.aar"))
        into "../release/jar/${endName}"
    }

    task "unZipClasses${moduleName[i]}"(type: Copy, dependsOn: ["unZipAAR${moduleName[i]}"]) {
        from(zipTree("../release/jar/${endName}/classes.jar"))
        into "../release/jar/${endName}/classes/"
    }

    task "unZipJar${moduleName[i]}"(type: Copy, dependsOn: ["unZipClasses${moduleName[i]}"]) {
        from("../release/jar/${endName}/classes/")
        from("../release/jar/${endName}") {
            include('assets/')
        }
        into "../release/jar/tmp/${endName}/"

        includeEmptyDirs = false
    }


    task "jar${moduleName[i]}"(type: Jar, dependsOn: ["unZipJar${moduleName[i]}"]) {
        from("../release/jar/tmp/${endName}/")
        baseName = "${endName}".replace("_${rootProject.ext.version}", "")
        destinationDir = file("../release/jar/")


        manifest {
            attributes(
                    'Implementation-Title': "${project.name}",
                    'Implementation-Version': "${rootProject.ext.version}",
                    'Built-Date': new Date().getDateTimeString(),
                    'Built-With': "gradle-${project.getGradle().getGradleVersion()},groovy-${GroovySystem.getVersion()}",
                    'Created-By': 'Java ' + System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')')
        }
    }

    task "jarclean${moduleName[i]}"(type: Delete, dependsOn: ["jar${moduleName[i]}"]) {

        delete("../release/jar/${endName}")
        delete("../release/jar/tmp")
    }
}

task makeJar() {

}

for (int i = 0; i < moduleName.size(); i++) {
    makeJar.dependsOn "jarclean${moduleName[i]}"
}

task makeDemo(type: Copy) {
    from("../../ans-demo/") {
        include("apidemo/")
        exclude("apidemo/build.gradle")
    }
    from("../../projectBuildTools/")

    into("../release/zip/demo")
}

task makeZipAns(type: Zip, dependsOn: ["makeDemo"]) {

    from("../release/zip/") {
        include("demo/")
    }
    from("../release/") {
        include("aar/")
        include("jar/")
    }

//    from("../../") {
//        include("repo/")
//    }

    baseName = "analysys-pass-android-${releaseTimeVariable}-${rootProject.ext.version}"
    destinationDir file("../release/zip")

}

task makeZipAnsClean(type: Delete, dependsOn: ["makeZipAns"]) {
    delete("../release/zip/demo/")
    delete("../release/aar/")
    delete("../release/jar/")
}


